ORG 0000H
AJMP MAIN
ORG 0030H

MAIN:
	MOV SP,#60H	 //初始化
	CLR F0
	SETB P1.2
	LCALL DISCLR
LOOP:
	SETB P1.1
	JB P1.1,GOON
WAITO:
	SETB P1.1
	JNB P1.1,WAITO	//等待CLEAR按键释放
	LCALL DISCLR
	CLR F0
GOON:
	SETB P1.0
	JB P1.0,NEXT1
WAIT1:
	SETB P1.0	   //等待Start/Stop按键释放
	JNB P1.0,WAIT1
	CPL F0
NEXT1:
	JNB F0,NEXT
	CLR P1.2	  //执行秒表计时功能
	LCALL DISPLAY
	LCALL DELAY10ms
	LCALL ADJUST
	SJMP LOOP
NEXT:
	SETB P1.2
	SJMP LOOP
DELAY10ms:		//10ms延时程序
	DL0:MOV R2,#100
	DL1:MOV R1,#48
	DL2:DJNZ R1,DL2	  //内循环100us
		NOP
		DJNZ R2,DL1	  //中循环10ms
		RET
ADJUST:			   //秒表计数器调整子程序
	INC 30H
	MOV A,30H
	CJNE A,#10,GOTORET
	MOV 30H,#0
	INC 31H
	MOV A,31H
	CJNE A,#10,GOTORET
	MOV 31H,#0
	INC 32H
	MOV A,32H
	CJNE A,#10,GOTORET
	MOV 32H,#0
	INC 33H
	MOV A,33H
	CJNE A,#10,GOTORET
	MOV 33H,#0
GOTORET:RET
DISCLR:			//显示清零
	MOV 30H,#0
	MOV 31H,#0
	MOV 32H,#0
	MOV 33H,#0
	CLR A
	MOV DPTR,#TABLE
	MOVC A,@A+DPTR
	MOV DPTR,#D10msADD
	MOVX @DPTR,A
	MOV DPTR,#D100msADD
	MOVX @DPTR,A
	MOV DPTR,#D1sADD
	MOVX @DPTR,A
	MOV DPTR,#D10sADD
	MOVX @DPTR,A
	SETB P1.2
	RET
DISPLAY:	   //显示子程序
	MOV A,30H
	MOV DPTR,#TABLE
	MOVC A,@A+DPTR
	MOV DPTR,#D10msADD
	MOVX @DPTR,A
	MOV A,31H
	MOV DPTR,#TABLE
	MOVC A,@A+DPTR
	MOV DPTR,#D100msADD
	MOVX @DPTR,A
	MOV A,32H
	MOV DPTR,#TABLE
	MOVC A,@A+DPTR
	MOV DPTR,#D1sADD
	MOVX @DPTR,A
	MOV A,33H
	MOV DPTR,#TABLE
	MOVC A,@A+DPTR
	MOV DPTR,#D10sADD
	MOVX @DPTR,A
	RET

TABLE:
	DB 0C0H,0F9H,0A4H,0B0H,99H,92H,82H,0F8H,80H,90H	//7段LED共阳显示码
	D10msADD EQU 0FE00H	  //x0.01s显示锁存地址
	D100msADD EQU 0FD00H  //x0.1s显示锁存地址
	D1sADD EQU 0FB00H	  //x1s显示锁存地址
	D10sADD EQU 0F700H	  //x10s显示锁存地址
END

