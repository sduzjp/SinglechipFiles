ORG 0000H
AJMP MAIN
ORG 0030H

MAIN:
	MOV SP,#60H
	MOV P1,#00H
LOOP:
	MOV 30H,#0		 //存放有无按键按下的标志
	MOV 31H,#0		 //存放键码
	LCALL KEYSCAN
	MOV A,30H
	JZ LOOP
	MOV 32H,#0		 //存放按键是否错误，1表示有错误，0表示无错误
	MOV 33H,#0		 //存放键号
	LCALL SEARCHKEYNUM
	MOV A,32H
	JNZ LOOP
	MOV DPTR,#TABLE2
	MOV A,33H
	MOVC A,@A+DPTR
	MOV P1,A
	LJMP LOOP
KEYSCAN:
	MOV P3,#0F0H
	MOV A,P3
	ANL A,#0F0H
	CJNE A,#0F0H,ASKEY
	SJMP KEYSCAN
ASKEY:
	LCALL DELAY10ms
	MOV P3,#0F0H
	MOV A,P3
	ANL A,#0F0H
	CJNE A,#0F0H,ISKEY
	MOV 31H,#0
	MOV 30H,#0
	LJMP TORET1
ISKEY:
	MOV 31H,A
SEARCHLINE0:
	MOV P3,#0FEH
	MOV A,P3
	ANL A,#0F0H
	CLR C
	SUBB A,#0F0H
	JZ SEARCHLINE1
	MOV A,#0EH
	LJMP TOSTORE
SEARCHLINE1:
	MOV P3,#0FDH
	MOV A,P3
	ANL A,#0F0H
	CLR C
	SUBB A,#0F0H
	JZ SEARCHLINE2
	MOV A,#0DH
	LJMP TOSTORE
SEARCHLINE2:
	MOV P3,#0FBH
	MOV A,P3
	ANL A,#0F0H
	CLR C
	SUBB A,#0F0H
	JZ SEARCHLINE3
	MOV A,#0BH
	LJMP TOSTORE
SEARCHLINE3:
	MOV P3,#0F7H
	MOV A,P3
	ANL A,#0F0H
	CLR C
	SUBB A,#0F0H
	JZ WRONGKEY
	MOV A,#07H
	LJMP TOSTORE
WRONGKEY:
	MOV 31H,#0
	MOV 30H,#0
	SJMP TORET1
TOSTORE:
	ORL A,31H
	CPL A
	MOV 31H,A
	MOV 30H,#1
TORET1:
	RET
SEARCHKEYNUM:
	MOV DPTR,#TABLE1
	MOV A,33H
	MOVC A,@A+DPTR
	CLR C
	SUBB A,31H
	JZ ENDSEARCH
	INC 33H
	MOV A,33H
	CJNE A,#10H,SEARCHKEYNUM
	MOV 32H,#1		 //没有找到相同的键码，说明是错误按键，如按双键
	SJMP TORET2
ENDSEARCH:
	MOV 32H,#0
TORET2:
	RET
DELAY10ms:
	DL0:MOV R2,#100
	DL1:MOV R1,#48
	DL2:DJNZ R1,DL2
		NOP 
		DJNZ R2,DL1
		RET

TABLE1:DB 11H,21H,41H,81H,12H,22H,42H,82H,14H,24H,44H,84H,18H,28H,48H,88H
TABLE2:DB 3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH,77H,7CH,39H,5EH,79H,71H
END